; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "RifidiEdge"
#define MyAppVersion "3.1.0"
#define MyAppPublisher "Transcends, LLC"
#define MyAppURL "http://www.transcends.co/"
#define MyAppExeName "rifidiserver.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A0049169-D7A4-45D5-B4B1-E552C0BF9BCC}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}-{#MyAppVersion}
DefaultGroupName={#MyAppName}
LicenseFile=C:\Users\matt\Desktop\finalinstall\install\server\license.txt
OutputBaseFilename={#MyAppName}-{#MyAppVersion}-windows-installer
SetupIconFile=C:\Users\matt\Desktop\finalinstall\installation\icon.ico
Compression=lzma
SolidCompression=yes
UsePreviousAppDir=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; Source: "C:\Users\rifidi\Desktop\finalinstall\install\server\rifidiserver.exe"; DestDir: "{app}"; Flags: ignoreversion
; Source: "C:\Users\rifidi\Desktop\finalinstall\install\client\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; Source: "C:\Users\rifidi\Desktop\finalinstall\install\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\matt\Desktop\finalinstall\install\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[code]
var
  RegPage: TInputQueryWizardPage;
procedure InitializeWizard();
begin
  RegPage := CreateInputQueryPage(wpWelcome,
  'Optional Email Registration', 'Who are you?',
  'First and last names are optional to register, but recommended.  '#13#10'Register now to: '#13#10'-Receive the Rifidi newsletter'#13#10'Get product and features updates'#13#10'-See the latest products and features of the Rifidi community');

  // Add items (False means it's not a password edit)
  RegPage.Add('Email:', False);
  RegPage.Add('First Name:', False);
  RegPage.Add('Last Name:', False);
end;

function GetEmail(): string;
begin
  result := RegPage.Values[0];
end;

function GetFirstName(): string;
begin
  result := RegPage.Values[1];
end;

function GetLastName(): string;
begin
  result := RegPage.Values[2];
end;

function ValidateEmail(strEmail : String) : boolean;
var
    strTemp  : String;
    nSpace   : Integer;
    nAt      : Integer;
    nDot     : Integer;
begin
    strEmail := Trim(strEmail);
    nSpace := Pos(' ', strEmail);
    nAt := Pos('@', strEmail);
    strTemp := Copy(strEmail, nAt + 1, Length(strEmail) - nAt + 1);
    nDot := Pos('.', strTemp) + nAt;
    Result := ((nSpace = 0) and (1 < nAt) and (nAt + 1 < nDot) and (nDot < Length(strEmail)));
end;

procedure CurStepChanged(CurStep : TSetupStep);
var
  WinHttpReq: Variant;
  url: String;
  email,lastname,firstname: String;
  ErrCode: integer;
begin
  if CurStep=ssPostInstall then
  begin
    if ValidateEmail(GetEmail()) then
    begin
      ShellExec('open', 'http://www.transcends.co/rifidi-survey?ref=%27Edge%27', '', '', SW_SHOW, ewNoWait, ErrCode);
      email:=GetEmail();
      firstname:=GetFirstName();
      lastname:=GetLastName();
      url:= 'http://us2.api.mailchimp.com/1.3/?method=listSubscribe&apikey='+'cbd2b79ccdb3872b19fa3f6b78f5e6a8&id=adadc9f492'+'&email_address='+email+'&merge_vars[FNAME]='+firstname+'&merge_vars[LNAME]='+lastname+'&output=xml&double_optin=false';
      WinHttpReq := CreateOleObject('WinHttp.WinHttpRequest.5.1');
      WinHttpReq.Open('POST', url, false);
      WinHttpReq.SetRequestHeader('Content-Type', 'application/x-www-form-urlencoded');
      WinHttpReq.Send(Email);
    end;
  end;
end;

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\server\{#MyAppExeName}"
Name: "{group}\Workbench"; Filename: "{app}\client\workbench.exe"
Name: "{group}\User's Guide"; Filename: "{app}\server\docs\Rifidi Edge Server Developer.pdf"
Name: "{group}\Developer's Guide"; Filename: "{app}\server\docs\Rifidi Edge Server User.pdf"
Name: "{group}\uninstall"; Filename: "{app}\unins000.exe"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\server\{#MyAppExeName}"; Tasks: desktopicon
Name: "{commondesktop}\Workbench"; Filename: "{app}\client\workbench.exe"; Tasks: desktopicon

